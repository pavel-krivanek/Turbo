"
I represent the border of a BlElement.

Example:

BlBorder builder
	paint: Color red;
	width: 10;
	build
"
Class {
	#name : #BlBorder,
	#superclass : #Object,
	#traits : 'TBlDebug',
	#classTraits : 'TBlDebug classTrait',
	#instVars : [
		'paint',
		'width',
		'style',
		'opacity'
	],
	#classInstVars : [
		'empty'
	],
	#category : #'Bloc-Basic-Border'
}

{ #category : #'instance creation' }
BlBorder class >> builder [
	^ BlBorderBuilder new
]

{ #category : #cleanup }
BlBorder class >> cleanUp [
	super cleanUp.
	
	empty := nil
]

{ #category : #accessing }
BlBorder class >> empty [
	^ empty ifNil: [ empty := self paint: BlPaint transparent width: 0 ]
]

{ #category : #'instance creation' }
BlBorder class >> new [
	self error: 'Use one of the builder methods'
]

{ #category : #'instance creation' }
BlBorder class >> paint: aPaint [

	^ self builder
		paint: aPaint;
		build
]

{ #category : #'instance creation' }
BlBorder class >> paint: aPaint width: aNumber [

	^ self builder
		paint: aPaint;
		width: aNumber;
		build
]

{ #category : #copy }
BlBorder >> copyWithOpacity: aNumber [
	self
		assert: [ aNumber isNotNil ]
		description: [ 'Opacity must not be nil' ].

	^ self class basicNew
		initialize;
		paint: paint;
		style: style;
		width: width;
		opacity: aNumber;
		yourself
]

{ #category : #copy }
BlBorder >> copyWithPaint: aPaint [

	self
		assert: [ aPaint isNotNil ]
		description: [ 'Paint must not be nil' ].

	^ self class basicNew
		initialize;
		paint: aPaint;
		style: style;
		width: width;
		opacity: opacity;
		yourself
]

{ #category : #copy }
BlBorder >> copyWithStyle: aStrokeStyle [

	self
		assert: [ aStrokeStyle isNotNil ]
		description: [ 'Style must not be nil' ].

	^ self class basicNew
		initialize;
		paint: paint;
		style: aStrokeStyle;
		width: width;
		opacity: opacity;
		yourself
]

{ #category : #copy }
BlBorder >> copyWithWidth: aWidth [

	self
		assert: [ aWidth isNotNil ]
		description: [ 'Width must not be nil' ].

	^ self class basicNew
		initialize;
		paint: paint;
		style: style;
		width: aWidth;
		opacity: opacity;
		yourself
]

{ #category : #testing }
BlBorder >> isTransparent [
	"Return true if I am is transparent and should not be rendered,
	false otherwise"
	<return: #Boolean>

	^ width isZero
]

{ #category : #testing }
BlBorder >> isVisible [
	"Return true if I am visible and should be rendered, false otherwise.
	Note: Non-zero border with transparent paint is still needs to be rendered,
	since it removes an overlapping background area"
	<return: #Boolean>

	^ width isZero not
]

{ #category : #accessing }
BlBorder >> opacity [
	<return: #Number>

	^ opacity
]

{ #category : #accessing }
BlBorder >> opacity: aNumber [
	self
		assert: [ aNumber isNotNil ]
		description: [ 'Opacity must not be nil' ].
	self
		assert: [ aNumber between: 0.0 and: 1.0 ]
		description: [ 'Opacity value must be within interval [0..1]' ].

	opacity isNotNil
		ifTrue: [ (BlImmutableObjectChangeError object: self) signal ].

	opacity := aNumber
]

{ #category : #accessing }
BlBorder >> paint [
	<return: #BlPaint>

	^ paint
]

{ #category : #accessing }
BlBorder >> paint: aBlPaint [
	self
		assert: [ aBlPaint isNotNil ]
		description: [ 'Paint must not be nil' ].

	paint isNotNil
		ifTrue: [ (BlImmutableObjectChangeError object: self) signal ].

	paint := aBlPaint asBlPaint
]

{ #category : #accessing }
BlBorder >> style [
	<return: #BlStrokeStyle>

	^ style
]

{ #category : #accessing }
BlBorder >> style: aBlStrokeStyle [
	self
		assert: [ aBlStrokeStyle isNotNil ]
		description: [ 'Style must not be nil' ].

	style isNotNil
		ifTrue: [ (BlImmutableObjectChangeError object: self) signal ].

	style := aBlStrokeStyle
]

{ #category : #accessing }
BlBorder >> width [
	<return: #Number>

	^ width
]

{ #category : #accessing }
BlBorder >> width: aNumber [
	self
		assert: [ aNumber isNotNil ]
		description: [ 'Width must not be nil' ].
	self
		assert: [ aNumber >= 0 ]
		description: [ 'Width vmust not be negative' ].

	width isNotNil
		ifTrue: [ (BlImmutableObjectChangeError object: self) signal ].
	
	width := aNumber
]
